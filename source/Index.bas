B4J=true
Group=Handlers
ModulesStructureVersion=1
Type=Class
Version=10.2
@EndOfDesignText@
Sub Class_Globals
	Private Request As ServletRequest
	Private Response As ServletResponse
	Private Method As String
	Private Elements() As String
	Private ElementKey As String
	Private HRM As HttpResponseMessage
End Sub

Public Sub Initialize
	HRM.Initialize
End Sub

Sub Handle (req As ServletRequest, resp As ServletResponse)
	Request = req
	Response = resp
	Method = Request.Method.ToUpperCase
	Dim FullElements() As String = WebApiUtils.GetUriElements(Request.RequestURI)
	Elements = WebApiUtils.CropElements(FullElements, 1) ' 1 For Index handler
	If ElementMatch("") Then
		If Main.App.MethodAvailable(Method, "", "index") Then
			ShowIndexPage
			Return
		End If
	End If
	If ElementMatch("key") Then
		Select ElementKey
			Case "modal"
				If Main.App.MethodAvailable2(Method, "/modal", Me) = False Then
					WebApiUtils.ReturnHtmlMethodNotAllowed(Response)
					Return
				End If
				WebApiUtils.ReturnHtml(GenerateModal, Response)
				Return
			Case "api"
				If Main.App.MethodAvailable2(Method, "/api/*", Me) = False Then
					WebApiUtils.ReturnMethodNotAllow(HRM, Response)
					Return
				End If
				HRM.ResponseCode = 200
				HRM.ResponseMessage = "Success"
				HRM.ResponseData = Array()
				WebApiUtils.ReturnHttpResponse(HRM, Response)
				Return
		End Select
	End If
	WebApiUtils.ReturnHtmlPageNotFound(Response)
End Sub

Private Sub ElementMatch (Pattern As String) As Boolean
	Select Pattern
		Case ""
			If Elements.Length = 0 Then
				Return True
			End If
		Case "key"
			If Elements.Length = 1 Then
				ElementKey = Elements(0)
				Return True
			End If
	End Select
	Return False
End Sub

Private Sub ShowIndexPage
	Dim doc As Document
	doc.Initialize
	doc.AppendDocType
	doc.Append(GenerateIndex)
	Dim content As String = doc.ToString
	'LogColor(content, -16776961)
	WebApiUtils.ReturnHTML(content, Response)
End Sub

Sub GenerateIndex As String
	Dim html1 As Tag = Html.lang("en")
	html1.comment(" generated by MiniHTML ")
	Head.up(html1).responsive.title("HTMX Demo") _
	.link("https://cdn.jsdelivr.net/npm/bootstrap@5.3.8/dist/css/bootstrap.min.css", "stylesheet", _
	"sha384-sRIl4kxILFvY47J16cr9ZwB07vP4J8+LH7qKQnuqkuIAvNWLzeN8tE5YBujZqJLB", "anonymous") _
	.script("https://cdn.jsdelivr.net/npm/bootstrap@5.3.8/dist/js/bootstrap.bundle.min.js", _
	"sha384-FKyoEForCGlyvwx9Hj09JcYn3nv7wiPVlz7YYwJrWVcXK/BmnVDxM+D2scQbITxI", "anonymous") _
	.script("https://cdn.jsdelivr.net/npm/htmx.org@2.0.7/dist/htmx.min.js", _
	"sha384-ZBXiYtYQ6hJ2Y0ZNoYuI+Nq5MqWBr+chMrS/RkXpNzQCApHEhOt2aY8EJgqwHLkJ", "anonymous")

	Dim body1 As Tag = Body.up(html1)
	
	Dim div1 As Tag = Div.addClass("d-flex align-items-center")
	div1.addStyle("height: 100vh")
	div1.up(body1)
	
	Dim div2 As Tag = Div.addStyle("margin: auto").up(div1)
	
	Dim div3 As Tag = Div.addId("modals-here") _
	.addClass("modal modal-blur fade") _
	.addStyle("display: none") _
	.attribute("aria-hidden", "false") _
	.attribute("tabindex", "-1").up(div2)
	
	Dim div4 As Tag = Div.addClass("modal-dialog modal-lg modal-dialog-centered") _
	.attribute("role", "document").up(div3)

	Div.addClass("modal-content").up(div4)
	
	Button.addClass("btn btn-primary text-uppercase") _
	.text("Open Modal") _
	.hxPost("/modal") _
	.hxTarget("#modals-here") _
	.hxTrigger("click") _
	.attribute("data-bs-toggle", "modal") _
	.attribute("data-bs-target", "#modals-here") _
	.up(div2).uniline

	Return html1.Build
End Sub

Sub GenerateModal As String
	Dim div4 As Tag = Div.addClass("modal-dialog modal-lg modal-dialog-centered")
	Dim div5 As Tag = Div.addClass("modal-content").up(div4)
	Dim div6 As Tag = Div.addClass("modal-header").up(div5)
	H5.addClass("modal-title").Text("Modal title").up(div6)
	Dim div6 As Tag = Div.addClass("modal-body").up(div5)
	Paragraph.Text("Modal body text goes here.").uniline.up(div6)
	Dim div6 As Tag = Div.addClass("modal-footer").up(div5)
	Button.addType("button").addClass("btn btn-secondary text-uppercase") _
	.attribute("data-bs-dismiss", "modal") _
	.text("Close").up(div6).uniline
	Return div4.Build
End Sub